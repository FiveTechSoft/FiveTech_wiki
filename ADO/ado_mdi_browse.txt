In this example we use MySQL and the FWH ADO API to create an ADO browse from a MDI child window:

mdi.prg
<code clipper>
#include "FiveWin.ch"
#include "adodef.ch"

static oWnd, oCn

//----------------------------------------------------------------------------//

function Main()

   local oBar

   SetDlgGradient( { { 1, RGB( 199, 216, 237 ), RGB( 237, 242, 248 ) } } )

   ADOCONNECT oCn TO MYSQL SERVER localhost DATABASE FWH USER root // PASSWORD ...

   DEFINE WINDOW oWnd MDI
   
   DEFINE BUTTONBAR oBar OF oWnd 2007 SIZE 80, 80
   	
   DEFINE BUTTON OF oBar ACTION ChildWindow() PROMPT "Child" ;
      FILENAME "bitmaps/new.bmp" 	
      		
   DEFINE MSGBAR OF oWnd PROMPT "ADO tutorial" 2007         
            				
   ACTIVATE WINDOW oWnd MAXIMIZED
   
return nil   	

//----------------------------------------------------------------------------//

function ChildWindow()

   local oWndChild, oBar, oBrw, oRs

   oRs = TRecSet():New():Open( "customer", oCn )
   	
   DEFINE WINDOW oWndChild MDICHILD OF oWnd
   	
   DEFINE BUTTONBAR oBar OF oWndChild 2007 SIZE 80, 80
   	
   DEFINE BUTTON OF oBar PROMPT "Add" FILENAME "bitmaps\plus.bmp" ;
      ACTION oBrw:EditSource( .t. )
   
   DEFINE BUTTON OF oBar PROMPT "Edit" FILENAME "bitmaps\edit.bmp" ;
   	ACTION oBrw:EditSource()

   DEFINE BUTTON OF oBar PROMPT "Delete" FILENAME "bitmaps\delete.bmp" ;
   	ACTION oBrw:Delete()

   @ 0, 0 XBROWSE oBrw OF oWndChild DATASOURCE oRs AUTOCOLS AUTOSORT ;
      CELL LINES NOBORDER

   oBrw:nEditTypes = EDIT_GET
   oBrw:CreateFromCode()
   
   // oBrw:bEdit = { | oRec | MyEdit( oRec ) }	

   // @ r,c, SAY oBrw:oSeek VAR oBrw:cSeeek OF oBar SIZE whatever

   oWndChild:SetControl( oBrw )
   oWndChild:bPostEnd := { || oRs:Close() }

   ACTIVATE WINDOW oWndChild

return nil

//----------------------------------------------------------------------------//
</code>