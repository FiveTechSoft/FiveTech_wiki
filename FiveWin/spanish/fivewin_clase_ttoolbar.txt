====== Clase TToolBar ======

Soporte para el control estandar **ToolBar** de Win32\\
Hereda de TControl                                                         

====== DATOS ======

^  Nombre  ^  Descripción  ^
^nBtnWidth|El ancho de los botones|
^nBtnHeight|El alto de los botones|
^aButtons|Un arreglo con los codeblocks para las acciones y los tooltips de los botones|
^oImageList|El Objeto TImagelist asociado cn los botones|
^lTTBalloon|Verdadero si se quiere establecer el estilo balloon del tooltip|

====== METODOS ======

^  Nombre  ^  Descripción  ^
^New|Constructor de código fuente|
^AddButton|Anade un botón al toolbar|
^AddMenu|Añade un **menu button** al toolbar|
^AddSeparator|Añade un separador al toolbar|
^ChangeBitmap|cambia el bitmap para un botón del toolbar|
^Create|Crea the control|
^DelButton|Elimina el botón especificado del toolbar|
^EnableButton|Habilita o deshabilita el botón especificado|
^HideButton|Oculta el botón especificado|
^IsButtonHidden|Devuelve .T. si el botón esta oculto|
^IsChecked|Devuelve .T. si el botón esta señalado|
^SetChecked|Establece el estado **señalado** o **no señalado** del botón especificado|
^SetMessage|Establece el mensaje para el botón especificado|
^SetPressed|Establece el estado **presionado** o **no presionado** del botón especificado|
^SetTextSets|Establece el texto del botón especificado|
^SetTextRows|Establece el numero máximo de filas de texto mostradas en el botón|
^SetTooltip|Establece el **tooltip** para el boton especificado|
^ShowButton|Muestra el botón especificado|
^ToggleCheck|Cambia el estado de **señalado** del botón especificado|

====== Ejemplo ======
<code clipper>
#include "FiveWin.ch"

static oToolBar

function Main()

   local oWnd, oImageList

   DEFINE WINDOW oWnd TITLE "FWH - Testing Win32 Toolbars" ;
      MENU BuildMenu()

   // First we build an ImageList with all the bitmaps 
   DEFINE IMAGELIST oImageList SIZE 32, 32

   DEFINE IMGBITMAP OF oImageList NAME "new"      COLOR nRGB( 255, 0, 255 )
   DEFINE IMGBITMAP OF oImageList NAME "open"     COLOR nRGB( 255, 0, 255 )
   DEFINE IMGBITMAP OF oImageList NAME "check"    COLOR nRGB( 255, 0, 255 )
   DEFINE IMGBITMAP OF oImageList NAME "search"   COLOR nRGB( 255, 0, 255 )
   DEFINE IMGBITMAP OF oImageList NAME "print"    COLOR nRGB( 255, 0, 255 )
   DEFINE IMGBITMAP OF oImageList NAME "internet" COLOR nRGB( 255, 0, 255 )
   DEFINE IMGBITMAP OF oImageList NAME "keys"     COLOR nRGB( 255, 0, 255 )
   DEFINE IMGBITMAP OF oImageList NAME "quit"     COLOR nRGB( 255, 0, 255 )

   // Now we create the toolbar and add the buttons
   
   DEFINE TOOLBAR oToolBar OF oWnd SIZE 50, 58 ; // 50, 50 ;
      IMAGELIST oImageList BALLOON // tooltips balloon style
   
   oToolBar:SetTextRows( 2 )
   
   DEFINE TBBUTTON OF oToolBar ;
      ACTION  oToolbar:SetText( 1, "Hello" ) ; // Modify first button text
      TOOLTIP "New" ;
      PROMPT  "&New project" ;
      MESSAGE "New"
   
   DEFINE TBBUTTON OF oToolBar ;
      ACTION  oToolBar:SetChecked( 2, .t. ) ; // Set pressed the second button 
      TOOLTIP "Open" ;
      PROMPT  "Open project" ;
      MESSAGE "Open"
      
   DEFINE TBSEPARATOR OF oToolBar
   
   DEFINE TBMENU OF oToolBar ;
      ACTION  MsgInfo( "Menu" ) ;
      TOOLTIP "Menu" ;
      PROMPT  "Menu" ;
      MENU    BuildPopup()
   
   DEFINE TBBUTTON OF oToolBar ;
      ACTION  oToolBar:SetChecked( 2, .f. ) ; // MsgInfo( "Search" ) ;
      TOOLTIP "Search" ;
      PROMPT  "Search"

   DEFINE TBBUTTON OF oToolBar ;
      ACTION  MsgInfo( "Print" ) ;
      TOOLTIP "Print a report" ;
      PROMPT  "Print a report"

   DEFINE TBSEPARATOR OF oToolBar
      
   DEFINE TBBUTTON OF oToolBar ;
      ACTION  MsgInfo( "Upgrade" ) ;
      TOOLTIP "Search for new versions" ;
      PROMPT  "Upgrade"
      
   DEFINE TBBUTTON OF oToolBar ;
      ACTION MsgInfo( "Users" ) ;
      TOOLTIP "Users management" ;
      PROMPT "Users"   

   DEFINE TBSEPARATOR OF oToolBar

   DEFINE TBBUTTON OF oToolBar ;
      ACTION  oWnd:End() ;
      TOOLTIP "End Application" ;
      PROMPT  "Exit"

   DEFINE STATUSBAR OF oWnd PROMPT "Toolbars test"

   ACTIVATE WINDOW oWnd MAXIMIZED
   
   oImageList:End()

return nil

function BuildMenu()

   local oMenu

   MENU oMenu
      MENUITEM "&Project"
      MENU
         MENUITEM "&New..." ACTION MsgInfo( "New" ) MESSAGE "New"
         MENUITEM "&Open..." ACTION MsgInfo( "Open" ) MESSAGE "Open"
         SEPARATOR
         MENUITEM "&Exit..." ACTION MsgInfo( "End" ) MESSAGE "Exit"
      ENDMENU

      MENUITEM "&Edit"
      MENU
         MENUITEM "&Search..." ACTION MsgInfo( "Search" )
         MENUITEM "&Print..." ACTION MsgInfo( "Print" )
      ENDMENU

      MENUITEM "&Utilities"
      MENU
         MENUITEM "&Disable button..." ACTION oToolBar:EnableButton( 3, .f. )
         MENUITEM "&Enable button..." ACTION oToolBar:EnableButton( 3, .t. )
      ENDMENU
   ENDMENU

return oMenu

function BuildPopup()

   local oMenu
   
   MENU oMenu POPUP
      MENUITEM "One"   ACTION MsgInfo( "One" )
      MENUITEM "Two"   ACTION MsgInfo( "Two" )
      MENUITEM "Three" ACTION MsgInfo( "Three" )
   ENDMENU
   
return oMenu      

procedure AppSys // XBase++ requirement

return
</code>

====== Vea tambien ======
[[fivewin_comando_toolbars|Comando ToolBar]]

====== Codigo fuente ======

FWH\SOURCE\CLASSES\TTOOLBAR.PRG
