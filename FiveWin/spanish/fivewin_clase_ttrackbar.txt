====== Clase TTrackBar ======

Soporte para el control estandar TrackBar de Win32\\
Hereda de TControl                                                         

====== DATOS ======

^  Nombre  ^  Descripción  ^
^nMin|La posicion mínima para el deslizador|
^nMax|La posicion máxima para el deslizador|
^nPos|La posicion actual del deslizador|
^nFreq|La frecuencia de intervalo de las marcas de graduación|

====== METODOS ======

^  Nombre  ^  Descripción  ^
^New|Constructor de código fuente|
^Redefine|Constructor de recursos|
^Change|Cambia el manejador de eventos|
^Default|Initialización predeterminada del control|
^EraseBkGnd|Borra el fondo del control|
^GetPos|Obtiene la posición actual del deslizador|
^Initiate|Inicializa el control|
^MouseMove|Manejador de eventos del movimiento del mouse|
^Notify|Manejador de eventos de notificación|
^SetPos|Establece la posición actual del deslizador|
^SetRange|Establece la posición mínima para el deslizador|
^SetTickFreq|Establece la frecuencia de intervalo de las marcas de graduación|

====== Ejemplo ======
<code clipper>
#include "FiveWin.ch"

#define TBS_AUTOTICKS           1
#define TBS_VERT                2
#define TBS_HORZ                0
#define TBS_TOP                 4
#define TBS_BOTTOM              0
#define TBS_LEFT                4
#define TBS_RIGHT               0
#define TBS_BOTH                8
#define TBS_ENABLESELRANGE      32
#define TBS_TOOLTIPS            256

//----------------------------------------------------------------------------//

function Main()

   local oWnd, oTrack1, oTrack2, oTrack3, oTrack4, oTrack5, oTrack6
   local oGet, nGet := 50

   DEFINE WINDOW oWnd TITLE "Testing Win32 TrackBar"

   @  30,  50 TRACKBAR oTrack1 OF oWnd PIXEL POS 50 ;
              ON CHANGE oGet:cText( nGet := oTrack1:GetPos() )

   @  80,  85 GET oGet VAR nGet OF oWnd PIXEL SIZE 30, 20 ;
              PICTURE "999" SPINNER MIN 0 MAX 100 ;
              ON CHANGE oTrack1:SetPos( nGet, .t. )

   @ 110,  50 TRACKBAR oTrack2 OF oWnd PIXEL TICKPOS TBS_BOTH ;
              WHEN nGet > 50

   @  30, 225 TRACKBAR oTrack3 OF oWnd PIXEL VERTICAL SIZE 40, 120

   @  30, 275 TRACKBAR oTrack4 OF oWnd PIXEL VERTICAL SIZE 40, 120 TICKPOS TBS_RIGHT

   @  30, 350 TRACKBAR oTrack5 OF oWnd PIXEL SELRANGE SIZE 150, 40 FREQUENCY 20

   @ 100, 350 TRACKBAR oTrack6 OF oWnd PIXEL SELRANGE SIZE 150, 40 FREQUENCY 20 ;
              TICKPOS TBS_BOTTOM

   @ 180, 200 BUTTON "From resources ..." OF oWnd PIXEL ;
              ACTION FromRes() SIZE 150, 30

   ACTIVATE WINDOW oWnd

return nil

//----------------------------------------------------------------------------//

static function FromRes()

   local oDlg, oTrack

   DEFINE DIALOG oDlg RESOURCE "TRACKBAR"

   REDEFINE TRACKBAR oTrack ID 101 OF oDlg

   ACTIVATE DIALOG oDlg CENTERED

return nil

//----------------------------------------------------------------------------//

procedure AppSys  // XBase++ requirement

return
</code>

====== Vea tambien ======
[[fivewin_comando_trackbars|commando trackbars]]

====== Codigo fuente ======

FWH\SOURCE\CLASSES\TTRACKBAR.PRG
