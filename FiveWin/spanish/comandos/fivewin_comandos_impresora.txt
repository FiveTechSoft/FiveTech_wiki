====== Usando la impresora ======
<code clipper>
 PRINT | PRINTER [ <oPrint> ] ;  
 [ TITLE | NAME | DOC <cDocument> ] ;  
 [ FROM USER ] ;  
 [ PREVIEW [ MODAL]];  
 [ TO <xModel> ]  
</code>
 

====== Parámetros ======
| <oPrint> | La referencia al objeto impresora |
| <cDocument> | El nombre del documento a generar. Es el nombre que aparece en la cola de impresión |
| <nRow>, <Ncol> | Las coordenadas de comienzo de la impresión medidas en unidades de resolución de la impresora | 

Diferentes impresoras generan impresiones con distinta apariencia. Para evitar esto (pues usted desea la misma apariencia en cualquier impresora!) primero ha de calcular la resolución de la impresora, y entonces, dividir esas dimensiones por el número de filas y columnas que usted desee:    

Revise SAMPLES\TestPrn2.prg    

| <cText> | The value to print. It may be of any type. FiveWin will automatically convert it into a string expression. |
| <oFont> | A font object to use when printing the <cText> value. See the FONT xBase commands to properly build a font. It is an optional parameter.|

Important: If you want a specific dimension for that font independently from the printer used, you must specify the OF ... clause when creating the font, and after the printer object has been created:  

<code clipper>
 local oPrn, oFont  

 PRINT oPrn  

    DEFINE FONT oFont NAME "Arial" SIZE 0, -8 OF oPrn  

    PAGE  

       ...  

    ENDPAGE  

 ENDPRINT  

 oFont:End()  
</code>
 
Important: You have to :End() the font object once you have finished and completed your printout work.  

| <nWidth> | A specific printer units width to use when printing the <cText> value. It is an optional parameter. |
| <nClrText> | An optional color to use when printing the <cText> value. It is an optional parameter. |

====== Claúsulas ======
| PAGE | Comienza una nueva página | 
| ENDPAGE | Finaliza la página en curso | 
| ENDPRINT | Termina la impresión | 
| FROM USER | Permite que el usuario seleccione interactivamente la impresora a usar. Si no usa esta cláusula, FiveWin usará la impresora instalada por defecto |
| PREVIEW | Muestra el sistema de previsualización de FiveWin, que le permite al usuario revisar la impresión por pantalla antes de enviarla a la impresora | 
| MODAL | Detiene la ejecución de la aplicación hasta que la ventana de previsualización se cierra | 

====== Ejemplos ======
\Fwh\Samples\TstPrn2.Prg
<code clipper>
// Sample showing how to manage the printer object

#include "FiveWin.ch"

static oWnd

function Main()

   local oPrn, oFont
   local nRowStep, nColStep
   local nRow := 0, nCol := 0, n, m

   // PrnSetSize( 2100, 1200 )     To adjust a different printer paper size!

   PRINT oPrn NAME "Testing the printer object from FiveWin" PREVIEW

      if Empty( oPrn:hDC )
         return nil          // Printer was not installed or ready
      endif

      DEFINE FONT oFont NAME "Ms Sans Serif" SIZE 0, -12 OF oPrn

      nRowStep = oPrn:nVertRes() / 20   // We want 20 rows
      nColStep = oPrn:nHorzRes() / 15   // We want 15 cols

      PAGE
         oPrn:SayBitmap( 1, 1, "..\bitmaps\fivewin.bmp" )
         for n = 1 to 20  // rows
             nCol = 0
             oPrn:Say( nRow, nCol, Str( n, 2 ), oFont )
             nCol += nColStep
             for m = 1 to 15
                oPrn:Say( nRow, nCol, "+", oFont )
                nCol += nColStep
             next
             nRow += nRowStep
         next
         oPrn:Line( 0, 0, nRow, nCol )
      ENDPAGE

      PAGE
         nRow = 0
         oPrn:SayBitmap( 1, 1, "..\bitmaps\fivewin.bmp" )
         for n = 1 to 20  // rows
             nCol = 0
             oPrn:Say( nRow, nCol, Str( n + 20, 2 ), oFont )
             nCol += nColStep
             for m = 1 to 15
                oPrn:Say( nRow, nCol, "+", oFont )
                nCol += nColStep
             next
             nRow += nRowStep
         next
         oPrn:Line( 0, 0, nRow, nCol )
      ENDPAGE

   ENDPRINT

   oFont:End()      // Destroy the font object

return nil
</code>

====== Imagen ======
{{:print.jpg|:print.jpg}}

====== Ver también ======
[[fivewin_class_tprinter|Class TPrinter]], [[fivewin_class_treport|Class TReport]]