====== Creating a MsgBar from source code (traditional dBase way) ======
<code clipper>
 SET MESSAGE [ OF <oWnd> ];  
    [ TO <cMsg> ];  
    [ CENTERED | CENTER ] ;  
    [ FONT <oFont> ] ;  
    [ CLOCK | TIME ] ;  
    [ DATE ] ;  
    [ KEYBOARD ] ;  
    [ COLOR | COLORS <nClrFore>;[,<nClrBack>] ] ;  
    [ NOINSET | NO INSET]  
</code> 

====== An alternative way (Objects creation way) ======
<code clipper>
 DEFINE MESSAGE [ BAR ] [ <oMsg> ];  
    [ OF <oWdn> ];  
    [ PROMPT <cMsg> ]  
    [ CENTERED | CENTER ] ;  
    [ FONT <oFont> ] ;  
    [ CLOCK | TIME> ] ;  
    [ DATE ] ;  
    [ KEYBOARD ] ;  
    [ COLOR | COLORS <nClrFore>;[,<nClrBack>] ] ;  
    [ NOINSET | NO INSET ]  
</code>

====== Defining yours own MsgItems ======
<code clipper>
 DEFINE MSGITEM [ <oMsgItem> ] ;  
    [ OF <oMsgBar> ] ;  
    [ PROMPT <cMsg> ] ;  
    [ SIZE <nSize> ] ;  
    [ FONT <oFont> ] ;  
    [ COLOR | COLORS <nClrFore>;[, <nClrBack>] ] ;  
    [ ACTION <uAction> ]  ;
    [ BITMAP, BITMAPS <cBitmap1> [, <cBitmap2> ] ] ;  
    [ TOOLTIP <cToolTip> ]  
</code> 

====== Parameters ======  
^ Parameter ^ Description                  ^
|<oWnd>|A reference to its container window|    
|<cPrompt>|The default msg to show|    
|<oFont>|An optional reference to a font object to use to display the msg| 
|<nClrFore>|Foreground color as a RGB number, or as a xBase string|    
|<nClrBack>|Background color as a RGB number|    
|<oMsgItem>|A reference of the MsgItem object|    
|<oMsgBar>|A reference of its container MsgBar|    
|<nSize>|Size of the MsgItem|    
|<uAction>|The action to perform when the mouse is clicked over|    
|<cBitmap1>, <cBitmap2>|Bitmaps to show for active and inactive state of the Msgitems|    
|<cToolTip>|Msgitem tooltip text|    

====== CLAUSES ======
^ Clause ^ Description                  ^
|CENTERED|If the text should show centered|    
|CLOCK , TIME|Time in the MsgBar|    
|DATE|Date in the MsgBar|    
|KEYBOARD|Keyboard status in the MsgBar|    
|NOINSET|For a non-inset MsgBar appearance|

====== Sample Code ======
<code clipper>
#include "FiveWin.ch"
//------------------------------------------------------//
Function Main()
   Local oWnd,oAppIcon,cAppUserName

   cAppUserNAme:="Fernandez"
   DEFINE ICON  oAppIcon RESOURCE "AppIcon"
   DEFINE WINDOW oWnd FROM 0,0  TO GetSysMetrics(1), GetSysMetrics(0) MDI ;
         TITLE "My Test Application" ;
         ICON  oAppIcon 

   SET MESSAGE OF oWnd TO " " 2007
   DEFINE MSGITEM OF oWnd:oMsgBar PROMPT "My Company Name" SIZE 160  // Company Name
   DEFINE MSGITEM OF oWnd:oMsgBar PROMPT "Location : UK" SIZE 90  // Branch Information
   DEFINE MSGITEM OF oWnd:oMsgBar PROMPT "User : "+cAppUserName SIZE 100   ACTION ChangeUser() // On Click change User Name
   DEFINE MSGITEM OF oWnd:oMsgBar PROMPT "Fn.Year : 2009-2010"SIZE 110 ACTION SelFnYear() // On click User function to change Fin.Year
   oWnd:oMsgBar:DateOn()  // displays Date
   oWnd:oMsgBar:ClockOn()  // Displays Time
   oWnd:oMsgBar:oClock:bMsg   := { || AMPM( Time() ) } // Displays Time in AmPm format
   oWnd:oMsgBar:oClock:nWidth += 20  // Increase width of Time column on Msg Bar

   ACTIVATE WINDOW oWnd 

RETURN NIL
oAppIcon:End()
</code>
====== Screen snapshot ======
{{:msgbar.jpg|:msgbar.jpg}}
====== See Also ======
[[fivewin_class_tmsgbar|Class TMsgBar]]    