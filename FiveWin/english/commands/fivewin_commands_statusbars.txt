====== StatusBar ======
**Defining a StatusBar **
<code clipper>
 DEFINE STATUSBAR [ OF <oStatusBar> ];  
 [ OF <oWnd> ];  
 [ PROMPT | TEXT <cText> ] ;  
 [ SIZES <aWidths> ] ;  
 [ PROMPTS <aTexts> ]  </code> 

| <oStatusBar> | A reference to the StatusBar object |
| <oWnd> | A reference to its container window | 
| <cText> | The caption of the StatusBar | 
| <aWidths> | An array of widths for the StatusBar items | 
| <aTexts> | An array of texts for the StatusBar items | 

====== Sample Code ======
\Fwh\Samples\StBar.Prg
<code clipper>
// Win32 common controls statusbar
#include "FiveWin.ch"
//---------------------------------//
Function Main()

   local oWnd, oStatusBar

   DEFINE WINDOW oWnd TITLE "Testing Win32 StatusBar" ;
      MENU BuildMenu()

   @ 2, 2 BUTTON "Set Part Text" SIZE 100, 20 ;
      ACTION oStatusBar:SetPartText( 2, "Works!" )

   @ 5, 2 BUTTON "Clock On" SIZE 100, 20 ;
      ACTION oStatusBar:ClockOn()

   DEFINE STATUSBAR oStatusBar PROMPT "  Win32 StatusBar" OF oWnd ;
      SIZES 150, 200, 240 ;
      PROMPTS "One", "Two", "Three"

   ACTIVATE WINDOW oWnd

return nil
//----------------------------------------//
function BuildMenu()

   local oMenu
   
   MENU oMenu
      MENUITEM "&One" MESSAGE "One"
      MENU
         MENUITEM "First"  MESSAGE "First"
         MENUITEM "Second" MESSAGE "Second"
      ENDMENU
      
      MENUITEM "&Two" MESSAGE "Two"
      MENUITEM "T&hree" MESSAGE "Three"
   ENDMENU
   
return oMenu   
</code>


====== Screen Snapshot ======
{{:statusbar.jpg|:statusbar.jpg}}

====== See also ======
[[fivewin_class_tstatusbar | Class TStatusBar ]]