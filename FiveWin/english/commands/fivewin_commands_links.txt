
====== Creating an URL Link from source code ======

<code clipper>
 @ <nRow>, <nCol> URLLINK [ <oURL> ] ;
 [ OF | WINDOW | DIALOG <oWnd> ] ;
 [ PIXEL ] ;
 [ DESIGN ] ;
 [ FONT <oFont> ] ;
 [ MESSAGE <cMsg> ] ;
 [ URL <cUrl> ] ;
 [ TOOLTIP <cToolTip> ] ;
 [ CLRINIT <nClrInit> ] ;
 [ CLROVER <nClrOver> ] ;
 [ CLRVISIT <nClrVisit> ]
</code>

====== Creating an URL Link from resources ======

<code clipper>
REDEFINE URLLINK [ <oURL> ] ;
 [ ID <nId> ] ;
 [ OF | WINDOW | DIALOG <oWnd> ] ;
 [ FONT <oFont> ] ;
 [ MESSAGE <cMsg> ] ;
 [ URL <cUrl> ] ;
 [ TOOLTIP <cToolTip> ] ;
 [ CLRINIT <nClrInit> ] ;
 [ CLROVER <nClrOver> ] ;
 [ CLRVISIT <nClrVisit> ]
</code>

====== DATAS ======

| <nRow>, <nCol> | The coordinates of the URL Link. They mimic text mode coordinates. |
| <oURL> | A reference to the URL Link Object. |
| <oWnd> | The container window of the URL Link. |
| <oFont> | A reference to the font object for the URL Link. |
| <cMsg> | The message associated with the control. |
| <cUrl> | The label text for the URL Link. |
| <cToolTip> | The tooltip text for the URL Link. |
| <nClrInit> | The default color for the URL Link. |
| <nClrOver> | The mouse over color for the URL Link. |
| <nClrVisit> | The visited color for the URL Link. |
| <nId> | The resource identifier of the URL Link. |



====== CLAUSES ======

| PIXEL | To use pixel coordinates instead of Text coordinates. |
| DESIGN | To activate design mode. |

====== Sample Code ======
<code clipper>
#include "FiveWin.ch"
#include "URLLink.ch"

#define COLOR_BTNFACE  15
//----------------------------------------------------------------------------//

function Main()

   local nColor := GetSysColor( COLOR_BTNFACE )
   local oDlg, oFont, oFnt1, oFnt2, oBmp

   DEFINE FONT oFont NAME GetSysFont() SIZE 0, -12
   DEFINE FONT oFnt1 NAME "ARIAL" SIZE 0, -20 BOLD
   DEFINE FONT oFnt2 NAME "ARIAL" SIZE 0, -14 BOLD

   DEFINE DIALOG oDlg RESOURCE "Main" FONT oFont

   REDEFINE BITMAP oBmp ID 301 OF oDlg RESOURCE "Fivewin"

   oBmp:cToolTip := "Logotipo de Fivewin"

   REDEFINE SAY ID 101 PROMPT "FW/FWH/FW++/FWC3" FONT oFnt1 ;
            COLOR CLR_BLACK, nColor OF oDlg

   REDEFINE SAY ID 102 PROMPT "Fivetech © 1993-2004" FONT oFnt2 ;
            COLOR CLR_MAGENTA, nColor OF oDlg

   REDEFINE SAY ID 103 ;
            PROMPT "La mejor librería para crear con CA-Clipper " + ;
                   "auténticas aplicaciones Windows. Es muy fácil " + ;
                   "de usar y los resultados son excelentes. " + ;
                   "Visite la página oficial de Fivewin:" ;
            FONT oFont OF oDlg

   REDEFINE URLLINK ID 104 URL "www.fivetechsoft.com" OF oDlg ;
            TOOLTIP "Sitio oficial de FiveTech"

   REDEFINE SAY ID 105 ;
            PROMPT "Fivewin para Harbour le ofrece el poder de " + ;
                   "los 32 bits. Además, ya está disponible FW " + ;
                   "para el nuevo compilador C3. " + ;
                   "Contacte con nosotros enviando un e-mail a:" ;
            FONT oFont OF oDlg

   REDEFINE URLLINK ID 106 URL "mailto:alinares@fivetechsoft.com" OF oDlg ;
            TOOLTIP "E-mail de FiveTech"

   REDEFINE BUTTON ID 201 ACTION ( oDlg:End() )

   oDlg:lHelpIcon := .f.

   ACTIVATE DIALOG oDlg CENTER ;
            VALID ( oFont:End(), oFnt1:End(), oFnt2:End(), .t. )

return nil

//----------------------------------------------------------------------------//
</code>

====== Screen Snapshot ======
{{:url.jpg|:url.jpg}}

====== Source code ======
FWH\include\urllink.ch


====== See Also ======
[[fivewin_class_turllink|Class TUrlLink]]