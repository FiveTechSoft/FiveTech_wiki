====== Selector =======
** Defining a SELECTOR object from source code: **
<code clipper>
 @ <nRow>, <nCol> SELECTOR [ <oSelec> VAR ] <nVar> ; 
 [ ORIGIN ANGLE <nAngle1> ] ;  
 [ LAST ANGLE <nAngle2> ] ;  
 [ RANGE <nMin>, <nMax> ] ;  
 [ MARKS <nMarks> ] ;  
 [ EXACT ] ;  
 [ OF | WINDOW | DIALOG <oDlg> ] ;  
 [ COLOR | COLORS <nClrFore>;[,<nClrBack>] ] ;  
 [ MESSAGE <cMsg> ] ;  
 [ DESIGN ] ;  
 [ PIXEL ] ;  
 [ SIZE <nWidth>, <nHeight> ] ;  
 [ UPDATE ] ;  
 [ ON CHANGE <uChange> ] ;  
 [ ON THUMBPOS <uPos> ]  
</code>
 **Redefining a SELECTOR object from resources: **
<code clipper>
 REDEFINE SELECTOR [ <oSelec> VAR ] <nVar> ; 
 [ ID <nId> ] ;  
 [ ORIGIN ANGLE <nAngle1> ] ;  
 [ LAST ANGLE <nAngle2> ] ;  
 [ RANGE <nMin>, <nMax> ] ;  
 [ MARKS <nMarks> ] ;  
 [ EXACT ] ;  
 [ OF | WINDOW | DIALOG <oDlg> ] ;  
 [ COLOR | COLORS <nClrFore>;[,<nClrBack>] ] ;  
 [ MESSAGE <cMsg> ] ;  
 [ UPDATE ] ;  
 [ ON CHANGE <uChange> ] ;  
 [ ON THUMBPOS <uPos> ]  </code>
** Parameters **
^ Parameter      ^ Description                                                           ^
| <nRow>, <nCol> | The coordinates of the Selector Object. We simulate Text coordinates. | 
| <oSelec> | A reference to the Selector Object. | 
| <nAngle1> | Origin angle of first selector mark in sexagesimals degree | 
| <nAngle2> | Origin angle of last selector mark in sexagesimals degree | 
| <nMin>, <nMax> | Selector range values | 
| <nMarks> | Number of selector marks | 
| <nClrFore>, <nClrBack> | Selector foreground and background colors | 
| <oWnd> | The container dialog of the Slider control. | 
| <cMsg> | The associated message of this control | 
| <nWidth>, <nHeigth> | The dimensions of the control. | 
| <nId> | The numeric resource identifier of the Slider Object. Is has to be assigned from the resource editor. |   
| <uChange> | An action to be performed when the selector changes | 
| <uPos> | An action to be performed when the selector is moved by the user and left on a different position. The new position is automatically provided as nPos value. |     

**CLAUSES **
^ Clause ^ Description                                                           ^
| EXACT  | When moving the selector it readjust itself to the the nearest mark strip. | 
| DESIGN | Allows the user to move and resize with the mouse | 
| PIXEL  | To use pixel coordinates instead of Text coordinates. | 
| UPDATE | This control may be updated from TDialog:Update() METHOD | 

====== Sample Code ======
\fwh\samples\testsele.prg 
<code clipper>
// Testing FiveWin selector controls
// Ramón Avendaño 1998

#include "FiveWin.ch"
#include "Selector.ch"


static oWnd

static oGet, oBtn, oSbr

static oSelec, oSelec1, oSelec2

//----------------------------------------------------------------------------//

function Main()

   local cTitle := "Testing the Selector controls"

   local oSay1, cSay1 := "Test"
   local oSay2, cSay2 := Space( 8 )

   local nVar1 := 130, nVar2, nVar3

   DEFINE WINDOW oWnd FROM 1, 1 TO 400, 600 PIXEL ;
      COLOR CLR_BLACK, CLR_HGRAY ;
      TITLE cTitle

   SET MESSAGE OF oWnd TO cTitle KEYBOARD CLOCK DATE NOINSET

   @ 20, 75 SAY oSay1 VAR cSay1 SIZE 80,18 PIXEL BORDER COLOR CLR_RED OF oWnd CENTERED
   @ 40, 75 SAY oSay2 VAR cSay2 SIZE 80,18 PIXEL BORDER COLOR CLR_RED OF oWnd

   @ 65, 20 SELECTOR oSelec VAR nVar1 OF oWnd ;
            ORIGIN ANGLE 240 ;     // en grados sexagésimales
            LAST ANGLE 120 ;       // estando el origen cero en el punto superior
            RANGE 100, 200 ;
            MARKS 11;
            SIZE 200, 150 PIXEL ;
            ON CHANGE oSay2:SetText( STR( nVar ) )
            // ON THUMBPOS oSay2:SetText( STR( nVar ) );


   @ 180, 30  SAY "Min." PIXEL OF oWnd
   @ 180, 180 SAY "Max." PIXEL OF oWnd

   @ 225, 70 BUTTON oBtn PROMPT "&Punto medio" SIZE 102,20 PIXEL ACTION oSelec:Set(150) OF oWnd

   @ 65, 240 SELECTOR oSelec1 VAR nVar2 OF oWnd ;
            RANGE 0, 10 ;
            MARKS 11;
            EXACT;
            SIZE 80, 80 PIXEL ;
            COLORS CLR_RED ;

  @ 140, 240 SELECTOR oSelec2 VAR nVar3 OF oWnd ;
            ORIGIN ANGLE 270 ;
            LAST ANGLE 90 ;
            RANGE 100, 0 ;  // Escala en el sentido contrario a la agujas del reloj
            MARKS 21;
            EXACT;
            ON CHANGE oSbr:SetPos( nVar ) ;
            ON THUMBPOS oSbr:SetPos( nVar ) ;
            SIZE 80, 80 PIXEL ;
            COLORS CLR_CYAN ;

  @ 260, 30 SCROLLBAR oSbr PIXEL OF oWnd;
            HORIZONTAL ;
            SIZE 300, 18 ;
            RANGE 0,100

   oSbr:SetPos( 100 )

   @ 120, 360 BUTTON "&Dialog" SIZE 92,22 PIXEL ACTION Test_Dialog() OF oWnd
   @ 160, 360 BUTTON "&Resource" SIZE 92,22 PIXEL ACTION Test_Resource() OF oWnd

   ACTIVATE WINDOW oWnd

return nil

//----------------------------------------------------------------------------//

Function Test_Dialog()

  local oDlg, oBmp

  local oSelecH, oSelecV
  local nVarH := 0, nVarV := 0;

  DEFINE DIALOG oDlg WINDOW oWnd TITLE "Test Selector control" ;
    FROM 50, 50 TO 400, 500 PIXEL

  @ 40, 40 BITMAP oBmp OF oDlg ;
           FILE "FiveWin.bmp" ;
           NO BORDER ;
           PIXEL ;
           ADJUST

  nVarH := 120
  nVarV := 115

  @ 5, 60 SELECTOR oSelecH VAR nVarH OF oDlg ;
               RANGE 50, 250 ;
               MARKS 15;
               ON CHANGE WndSetSize( oBmp:hWnd, nVar, nVarV, .t. ) ;
               ON THUMBPOS WndSetSize( oBmp:hWnd, nVar, nVarV, .t. ) ;
               SIZE 30, 30 PIXEL

  @ 60, 5 SELECTOR oSelecV VAR nVarV OF oDlg ;
                 RANGE 50, 250 ;
               MARKS 15;
               ON CHANGE WndSetSize( oBmp:hWnd, nVarH, nVar, .t. ) ;
               ON THUMBPOS WndSetSize( oBmp:hWnd, nVarH, nVar, .t. ) ;
               SIZE 30, 30 PIXEL

  ACTIVATE DIALOG oDlg ;
    ON INIT MoveWindow( oBmp:hWnd, 80, 80, nVarH, nVarV, .t. ) ;

return nil

//----------------------------------------------------------------------------//

Function Test_Resource()

  local oDlg

  local oSelec1, nVar1
  local oSelec2, nVar2
  local oSelec3, nVar3
  local oSelec4, nVar4

  DEFINE DIALOG oDlg RESOURCE "TEST_SELECTOR" WINDOW oWnd

  REDEFINE SELECTOR oSelec1 VAR nVar1 OF oDlg ID 101 ;
     MARKS 1

  REDEFINE SELECTOR oSelec2 VAR nVar2 OF oDlg ID 102 ;
     MARKS 9 ;
     EXACT ;
     COLOR CLR_BLACK, CLR_HGRAY, CLR_CYAN

  REDEFINE SELECTOR oSelec3 VAR nVar3 OF oDlg ID 103 ;
     MARKS 15 ;
     COLOR CLR_BLACK, CLR_HGRAY, CLR_YELLOW

  REDEFINE SELECTOR oSelec4 VAR nVar4 OF oDlg ID 104 ;
     MARKS 5 ;
     COLOR CLR_BLACK, CLR_HGRAY, CLR_GREEN

  ACTIVATE DIALOG oDlg

return nil
</code>

====== Screen Snapshot ======
{{:selector.jpg|:selector.jpg}}

====== See also ======
[[fivewin_class_tselector | Class TSelector ]]