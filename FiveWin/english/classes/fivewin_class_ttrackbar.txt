====== Class TTrackBar ======

Win32 TrackBar common control support\\                                          
Inherits from TControl                                                         

====== DATA ======
^ Data ^ Description                     ^
|nMin|The minimum position for the slider|
|nMax|The maximum position for the slider|
|nPos|The current position of the slider|
|nFreq|The interval frequency for tick marks|

====== METHODS ======
^ Method ^ Description                     ^
|New|Constructor from source code|
|Redefine|Constructor from resources|
|Change|Change event handler|
|Default|Default initialization of the control|
|EraseBkGnd|Erases the background of the control|
|GetPos|Retrieves the current position of the slider|
|Initiate|Initializes the control|
|MouseMove|Mouse move event handler|
|Notify|Notify event handler|
|SetPos|Sets the current position of the slider|
|SetRange|Sets the minimum and maximum position for the slider|
|SetTickFreq|Sets the interval frequency for tick marks|

====== Example ======
<code clipper>
#include "FiveWin.ch"

#define TBS_AUTOTICKS           1
#define TBS_VERT                2
#define TBS_HORZ                0
#define TBS_TOP                 4
#define TBS_BOTTOM              0
#define TBS_LEFT                4
#define TBS_RIGHT               0
#define TBS_BOTH                8
#define TBS_ENABLESELRANGE      32
#define TBS_TOOLTIPS            256

//----------------------------------------------------------------------------//

function Main()

   local oWnd, oTrack1, oTrack2, oTrack3, oTrack4, oTrack5, oTrack6
   local oGet, nGet := 50

   DEFINE WINDOW oWnd TITLE "Testing Win32 TrackBar"

   @  30,  50 TRACKBAR oTrack1 OF oWnd PIXEL POS 50 ;
              ON CHANGE oGet:cText( nGet := oTrack1:GetPos() )

   @  80,  85 GET oGet VAR nGet OF oWnd PIXEL SIZE 30, 20 ;
              PICTURE "999" SPINNER MIN 0 MAX 100 ;
              ON CHANGE oTrack1:SetPos( nGet, .t. )

   @ 110,  50 TRACKBAR oTrack2 OF oWnd PIXEL TICKPOS TBS_BOTH ;
              WHEN nGet > 50

   @  30, 225 TRACKBAR oTrack3 OF oWnd PIXEL VERTICAL SIZE 40, 120

   @  30, 275 TRACKBAR oTrack4 OF oWnd PIXEL VERTICAL SIZE 40, 120 TICKPOS TBS_RIGHT

   @  30, 350 TRACKBAR oTrack5 OF oWnd PIXEL SELRANGE SIZE 150, 40 FREQUENCY 20

   @ 100, 350 TRACKBAR oTrack6 OF oWnd PIXEL SELRANGE SIZE 150, 40 FREQUENCY 20 ;
              TICKPOS TBS_BOTTOM

   @ 180, 200 BUTTON "From resources ..." OF oWnd PIXEL ;
              ACTION FromRes() SIZE 150, 30

   ACTIVATE WINDOW oWnd

return nil

//----------------------------------------------------------------------------//

static function FromRes()

   local oDlg, oTrack

   DEFINE DIALOG oDlg RESOURCE "TRACKBAR"

   REDEFINE TRACKBAR oTrack ID 101 OF oDlg

   ACTIVATE DIALOG oDlg CENTERED

return nil

//----------------------------------------------------------------------------//
</code>

** Screen Snapshot **

{{:trackbar.jpg|:trackbar.jpg}}

====== See Also ======
[[fivewin_commands_trackbars | Command TrackBar ]], [[fivewin_class_tslider | Class TSlider ]], [[fivewin_commands_slider | Command Slider ]]
====== Source code ======
FWH\SOURCE\CLASSES\TTRACKBAR.PRG